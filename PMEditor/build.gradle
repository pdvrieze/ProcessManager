import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

/*
 * Copyright (c) 2018.
 *
 * This file is part of ProcessManager.
 *
 * ProcessManager is free software: you can redistribute it and/or modify it under the terms of version 3 of the
 * GNU Lesser General Public License as published by the Free Software Foundation.
 *
 * ProcessManager is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
 * the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License along with ProcessManager.  If not,
 * see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

version = '0.5.1'
description = 'Android interface with process model editor and task interface'

configurations {
    cleanedAnnotations

    compile.exclude group: 'org.jetbrains', module: 'annotations'
}

dependencies {
    implementation 'org.jetbrains:annotations-java5:15.0'

    implementation project(':PE-diagram:android')
    implementation project(':PE-common:android')
    implementation project(':darwinlib')

    implementation project(':JavaCommonApi:java')

    implementation("net.devrieze:xmlutil-android:$xmlutilVersion")
    implementation("net.devrieze:xmlutil-serialization-android:$xmlutilVersion")
    implementation project(':java-common:android')
    implementation 'net.devrieze:android-coroutines-appcompat:0.7.0'

    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "com.android.support:support-vector-drawable:${androidCompatVersion}"
//    kapt "com.android.databinding:compiler:$databindingVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'xmlunit:xmlunit:1.6'
    testImplementation project(':java-common:android')
    testImplementation 'net.sf.kxml:kxml2:2.3.0'

    implementation "com.android.support:appcompat-v7:${androidCompatVersion}"
    implementation "com.android.support:support-annotations:${androidCompatVersion}"
    implementation "com.android.support:design:${androidCompatVersion}"
    implementation "com.android.support:support-v4:${androidCompatVersion}"
    implementation "com.android.support:recyclerview-v7:${androidCompatVersion}"
    implementation "com.android.support:cardview-v7:${androidCompatVersion}"
    implementation 'com.android.support:multidex:1.0.3'

    def nav_version = "1.0.0-alpha02"

    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version" // use -ktx for Kotlin
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version" // use -ktx for Kotlin

    def lifecycle_version = "1.1.1"
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"

    kapt "android.arch.lifecycle:compiler:$lifecycle_version"

    cleanedAnnotations 'org.jetbrains:annotations-java5:15.0'
}

ext {
    if (!rootProject.hasProperty('androidTarget')) {
        androidTarget = '26'
    }
    if (!rootProject.hasProperty('androidCompatVersion')) {
        androidCompatVersion = '26.3.1'
    }
    if (!rootProject.hasProperty('androidPluginVersion')) {
//        androidPluginVersion = '2.2.2'
        androidPluginVersion = '3.1.1'
    }
}

android {
    compileSdkVersion androidTarget.toInteger()

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion androidTarget.toInteger()
        versionCode 2
        versionName version
        manifestPlaceholders = [serverScheme: 'https', serverHost: 'darwin.bournemouth.ac.uk']
//        jackOptions {
//            enabled = true
//        }
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility org.gradle.api.JavaVersion.VERSION_1_8
        targetCompatibility org.gradle.api.JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
        }
        debug {
//            multiDexEnabled true
//            minSdkVersion 21
            manifestPlaceholders = [serverScheme: 'http', serverHost: '10.0.2.2']
//            applicationIdSuffix ".debug"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-project.txt'
            versionNameSuffix "-debug"
        }
    }

    testOptions {
//        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
        exclude '**.kotlin_metadata'
        exclude 'nl/adaptivity/xmlutil/SubstreamFilterWriter.kotlin_metadata'
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }

}


tasks.withType(KotlinCompile) {

    kotlinOptions.freeCompilerArgs = [argJvmDefault]
}

task cleanAnnotationsJar(type: Jar, dependsOn: configurations.cleanedAnnotations) {
    archiveName = "annotations-cleaned.jar"
    exclude 'org/jetbrains/annotations/NotNull.class'
    exclude 'org/jetbrains/annotations/Nullable.class'

    doFirst {
        configurations.cleanedAnnotations.each { f ->
            project.logger.debug("Found ${f} in cleanedAnnotations")
            from zipTree(f)
        }
    }
}
