/*
 * Copyright (c) 2016.
 *
 * This file is part of ProcessManager.
 *
 * ProcessManager is free software: you can redistribute it and/or modify it under the terms of version 3 of the
 * GNU Lesser General Public License as published by the Free Software Foundation.
 *
 * ProcessManager is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
 * the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License along with ProcessManager.  If not,
 * see <http://www.gnu.org/licenses/>.
 */

pluginManagement {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }

        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == "kotlinx-serialization") {
                def version = (requested.version == null || requested.version.length() == 0) ? serializationPluginVersion : requested.version
/*
                // The kotlin version. Unfortunately intellij doesn't support the settings delegate (even though gradle does)
                val version = when {
                    !requested.version.isNullOrEmpty()
                         -> requested.version

                    else -> gradle.rootProject.extra["serializationVersion"]
                }
*/
                useModule("org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$version")

            } else if (requested.id.id == "net.devrieze.gradlecodegen") {
                useModule("net.devrieze:gradle-codegen:0.5.6")
            }
        }
    }
}

rootProject.name = "ProcessManager"
include(":multiplatform:common")
include(":multiplatform:common-nonshared")
include(":multiplatform:java")
include(":multiplatform:jvm")
include(":multiplatform:js")

include(":JavaCommonApi:common")
include(":JavaCommonApi:java")
include(":JavaCommonApi:jvm")
include(":JavaCommonApi:js")

if(file("kotlinsql").exists()) {
    includeBuild("kotlinsql") {
        dependencySubstitution {
            substitute module("net.devrieze:kotlinsql:") with project(":")
        }
    }
}

include(":java-common:common")
include(":java-common:jvm")
include(":java-common:java")
include(":java-common:js")

include(":DarwinJavaApi")

include(":TestSupport")

include(":PE-common:common")
include(":PE-common:jvm")
include(":PE-common:endpointDoclet")

include(":DarwinGenerators")
include(":darwin-sql")

include(":PEUserMessageHandler")

include(":ProcessEngine:core")
include(":ProcessEngine:servlet")

include(":DarwinClients")

include(":PE-dataservices")
include(":PE-diagram:common")
include(":PE-diagram:jvm")
include(":DarwinRealm")
include(":DarwinServices")
include(":PE-server")
include(":accountmgr")
include(":accountmgr:js")
include(":accountcommon")
include(":darwin:jvm")
include(":darwin:shared")
include(":darwin:js")
include(":structurizr")

if (file("xmlutil").exists()) {
    includeBuild("xmlutil") {
        dependencySubstitution {
            substitute module('net.devrieze:xmlutil-java') with project(':core:java')
            substitute module('net.devrieze:xmlutil-android') with project(':core:android')
            substitute module('net.devrieze:xmlutil-jvm') with project(':core:jvm')
            substitute module('net.devrieze:xmlutil-js') with project(':core:js')
            substitute module('net.devrieze:xmlutil-serialization-jvm') with project(':serialization:java')
            substitute module('net.devrieze:xmlutil-serialization-android') with project(':serialization:java')
        }
    }
}

include(":jscommon")
include(":webeditor")

include(":multiplatform:android")

// Android modules
if (Boolean.parseBoolean(androidEnabledProp)) {
    include(":java-common:android")
    include(":PE-common:android")
    include(":PE-diagram:android")

    include(":android-auth")
    include(":darwinlib")
    include(":PMEditor")
}
